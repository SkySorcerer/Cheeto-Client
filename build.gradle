buildscript {
    repositories {
        maven {
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

compileJava.options.encoding = 'UTF-8'
version = '1.0'
group = 'xaeroplus'
archivesBaseName = 'xaeroplus-1.12.2'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    jcenter()
    mavenCentral()
    mavenLocal()
}

minecraft {

    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20171003-1.12'

    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'xaeroplus.mixin.MixinLoaderForge,xaeroplus.dev.minimap.core.XaeroMinimapPlugin,xaeroplus.dev.worldmap.core.XaeroWorldMapPlugin'

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    jarLibs
    // Force choosing the correct nightly build because Mac OS chooses an invalid one
    all {
        resolutionStrategy {
            force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.0'

    jarLibs('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'launchwrapper'
        exclude module: 'log4j-core'
    }

    jarLibs 'org.apache.commons:commons-compress:1.21'

    implementation(fg.deobf("curse.maven:xaeros-world-map-317780:4424616"))
    implementation(fg.deobf("curse.maven:xaeros-minimap-263420:4430571"))

    annotationProcessor('org.spongepowered:mixin:0.8.5:processor') {
        exclude module: 'gson'
    }

    implementation configurations.jarLibs
}

sourceSets {
    wdl {
        compileClasspath += main.compileClasspath
    }
    main {
        ext.refMap = "mixins.xaeroplus.refmap.json"
        compileClasspath += wdl.output
    }
}

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, 'mixins.xaeroplus.refmap.json'
}

jar {
    manifest {
        attributes([
            'Manifest-Version': 1.1,
            'MixinConfigs': 'mixins.xaeroplus.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'xaeroplus.mixin.MixinLoaderForge',
            'ForceLoadAsMod': 'true',
            "Specification-Title": "XaeroPlus",
            "Specification-Vendor": "rfresh2",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"rfresh",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    // Copy needed libs to jar
    from {
        configurations.jarLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar.finalizedBy('reobfJar')

task setGameDir() {
    // Check if custom gamedir has been passed, if not use default ones per platform
    if (!project.hasProperty("gameDir")) {
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            project.ext.gameDir = System.getenv("APPDATA") + "/.minecraft"
        } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            project.ext.gameDir = System.properties["user.home"] + "/Library/Application Support/minecraft"
        } else {
            project.ext.gameDir = System.properties["user.home"] + "/.minecraft"
        }
    }
}

/**
 * Courtesy of ForgeHax.
 * I symlink my vanilla mc launcher folder for MultiMC
 * various hardcoded directories in here could be changed by preference if your main dir is a MultiMC one
 */
class CopyJarTask extends DefaultTask {
    CopyJarTask() {
        group = "build"
    }
    @TaskAction
    protected void exec() {
        project.copy {
            final gameDir = project.ext.gameDir;
            final modDir = "$gameDir/mods/1.12.2"

            // find previous jar (if it exists)
            final previousJars = project.fileTree(dir: modDir).matching {
                include project.archivesBaseName + '*'
            }.files

            // change file name
            if (previousJars) {
                // create .backups folder
                final backupDir = modDir + '/.backups/'
                project.file(backupDir).mkdirs()

                for (File previousJar : previousJars) {
                    final n = modDir + '/.backups/' + previousJar.getName() + '.bk'
                    def f = project.file(n)
                    def i = 1
                    while (f.exists()) {
                        f = project.file(n + '_' + i)
                        i++
                    }
                    previousJar.renameTo(f)
                }
            }

            from project.jar
            into "$gameDir/mods/1.12.2"
        }
    }
}

task copyJar(type: CopyJarTask, dependsOn: [setGameDir, build])
